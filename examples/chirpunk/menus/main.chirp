// The main menu screen with the hangar background
fn main_menu_item(name) {
    Entity(
        named(name) style(MainMenuItemButton)
        bloom(2.3) focusable row main_margin(10) rules(60%, 1.5*) distrib_start
    ) {
        Entity(text(name) style(MainMenuItemText))
    }
}
// The main menu background is animated by fadding in and out semitransparent
// images.
fn animated_bg(name, image, $offset, $period, $active_period) {
    Entity(
        named(name) rules(100%, 100%) image(image)
        // gyro((offset: $offset, period: $period, active_period: $active_period))
        gyro($offset, $period, $active_period)
    )
}
Root(root_menu swatch_target(Root) layout(">oSaE") screen_root image("images/main_menu/screen_10.png")) {
    animated_bg!(Blink1, "images/main_menu/screen_10_blink1.png", 5,    10,  10)
    animated_bg!(Blink2, "images/main_menu/screen_10_blink2.png", 0,    10,  10)
    animated_bg!(Blue,   "images/main_menu/screen_10_blue.png",   0.75, 1.5, 1)
    animated_bg!(Red,    "images/main_menu/screen_10_red.png",    0,    1.5, 1)(
        layout(">dSaS") main_margin(100)
    ) {
        Menu(column rules(420px, 100%) main_margin(40) image("images/main_menu/board.png")) {
            // TODO(feat): randomly flip x/y axis
            LogoShadow(row  image("images/main_menu/logo_shadow.png") rules(100%, 1*)) {
                Logo(image("images/main_menu/logo.png") width(100%))
            }
            main_menu_item!("CONTINUE")(prioritized)
            main_menu_item!("NEW GAME")
            main_menu_item!("LOAD GAME")
            main_menu_item!("SETTINGS")(swatch_target(Settings))
            main_menu_item!("ADDITIONAL CONTENT")
            main_menu_item!("CREDITS")
            main_menu_item!("QUIT GAME")(cancel)
            EmptyBottomMargin(rules(95%, 10%))
        }
    }
}
// vim: set syntax=ron
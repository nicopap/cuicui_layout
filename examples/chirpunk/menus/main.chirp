// The main menu screen with the hangar background

// -------- Consideration for future usage --------------
// We may support in the future `let` and `fn` keywords, as to define re-usable elements.
// We may also require in the future that there is exactly a single root item
// which would simplify somewhat the AssetLoader implementation, but also to
// add a `use [str_literal | ident [( ident[, ident ,]* )]?]` syntax to import
// and inline directly a single chirp.
// fn main_menu_item(name) {
//     row(main_margin 10., focusable, style MainMenuItem, image "images/main_menu/button.png") {
//         spawn(text name, style MainMenuItemText);
//     }
// }
// ------------------------------------------------------

Root(root_menu swatch_target(Root) layout(">dSaS") screen_root image("images/main_menu/screen_10.png")) {
// This 'gyro' nonsense is how the police car's strob light and the "crime scene"
// strip works.
 Blink1(
     full_screen image("images/main_menu/screen_10_blink1.png")
     gyro ((offset: 5, period: 10, active_period: 10))
 ) {
 Blink2(
     full_screen image("images/main_menu/screen_10_blink2.png")
     gyro ((offset: 0, period: 10, active_period: 10))
 ) {
 Blue(
     full_screen image("images/main_menu/screen_10_blue.png")
     gyro ((offset: 0.75, period: 1.5, active_period: 1))
 ) {
 Red(
     full_screen main_margin(100.) image("images/main_menu/screen_10_red.png")
     gyro ((offset: 0, period: 1.5, active_period: 1))
 ) {
     Menu(column rules(420px, 100%) main_margin(40) image("images/main_menu/board.png")) {
         // TODO(feat): randomly flip x/y axis
         LogoShadow(row  image("images/main_menu/logo_shadow.png") rules(100%, 1*)) {
             Logo(image("images/main_menu/logo.png") width(100%))
         }
         "CONTINUE"          (main_menu_item prioritized)
         "NEW GAME"          (main_menu_item)
         "LOAD GAME"         (main_menu_item)
         "SETTINGS"          (main_menu_item swatch_target(Settings))
         "ADDITIONAL CONTENT"(main_menu_item)
         "CREDITS"           (main_menu_item)
         "QUIT GAME"(cancel   main_menu_item)
         EmptyBottomMargin(rules(95%, 10%))
     }
 }
 }
 }
 }
}
// vim: set syntax=ron
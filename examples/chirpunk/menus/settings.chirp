// The settings menu
// fn tab_button(text) {
//     focusable(named text) {
//         spawn(text text, style TabButton);
//     }
// }
SettingsMenu(
    layout("vdSaS") root
    wrap scope menu("SETTINGS")
    image("images/settings/background.png")
    screen_root
) {
    // The header consists of (1) the "RETURN" button (2) each individual tab
    Header(row width(100%) height(10%) main_margin(50)) {
        Entity(column rules(100%, 100%)) {
            TabButtons(row rules(100%, 99%)) {
                RETURN(cancel text("RETURN") width(19.9%) style(BackText))
                TabBar(row width(79.9%) main_margin(100)) {
                    DISPLAY(row swatch_target(Display) prioritized tab_button)
                    "SOUND"(row swatch_target(Sound) tab_button)
                    "TAB 3"(row swatch_target(Tab3) tab_button)
                    "TAB 4"(row swatch_target(Tab4) tab_button)
                }
            }
            TabLineSeparator(bg(rgb(255, 98, 81)) rules(100%, 0.99%))
        }
    }
    // all tabs are actually loaded, but only one is shown at the same time
    // The logic is implemented by show_menus::Plugin.
    // Here, the `swatch_name` method inserts a SwatchBuilder component that
    // triggers the behaviors implemented by show_menus::Plugin
    AllTabs(layout(">oSaS") main_margin(130) cross_margin(50) swatch_name(SettingsSubmenu) rules(100%, 90%)) {
        DisplayMenu(column menu("DISPLAY") rules(100%, 100%) layout("vdSaC")) {
            Display(settings_header row width(100%) main_margin(40) distrib_start)
            Spacer(height(40px))
            "Window mode"(options(Choice(["Windowed","Borderless"])) row rules(100%,1*) settings_row)
            Spacer(height(20px))
            "Mouse lock"(options(Toggle) row rules(100%, 1*) settings_row)
            Spacer(height(20px))
            "Resizable window"(options(Toggle) row rules(100%, 1*) settings_row)
            Spacer(height(20px))
            "Resolution"(options(Choice(["1920x1080","1280x720","720x720"])) row rules(100%,1*) settings_row)
            Spacer(height(20px))
            "Profiler Overlay"(options(Toggle) row rules(100%,1*) settings_row)
        }
        SoundMenu(column menu("SOUND") rules(100%, 100%) layout("vdSaC")) {
            "Audio Settings"(settings_header row width(100%) main_margin(40) distrib_start)
            Spacer(height(40px))
            // column("SoundItems", rules(100%, 90%), cross_margin 80)
            // TODO(feat): add Interaction & Focusable to this
            "Master volume"(options(Increments(9)) row rules(100%, 1*) settings_row)
            Spacer(height(20px))

            "Effects volume"(options(Increments(9)) row rules(100%, 1*) settings_row)
            Spacer(height(20px))
            "Music volume"(options(Increments(9)) row rules(100%, 1*) settings_row)
        }
        Tab3Menu(column menu("TAB 3") rules(100%, 100%) layout("vdSaC")) {
            "Tab 3"(settings_header row width(100%) main_margin(40) distrib_start)
            Spacer(height(40px))
            "Sound effect 1"(options(Click) row rules(100%, 1*) settings_row)
            Spacer(height(20px))
            "Sound effect 2"(options(Click) row rules(100%, 1*) settings_row)
        }
        Tab4Menu(column menu("TAB 4") rules(100%, 100%) layout("vdSaC")) {
            "Tab 4"(settings_header row width(100%) main_margin(40) distrib_start)
            Spacer(height(40px))
            "Fine grained clicking"(options(Increments(30)) row rules(100%, 1*) settings_row)
            Spacer(height(20px))
            "UI highlight color"(settings_row row rules(100%,1*) options(Choice([
                "aliceblue", "antiquewhite", "aqua",           "aquamarine",
                "chocolate", "coral",        "cornflowerblue", "cornsilk",
                "darkgreen", "darkkhaki",    "darkmagenta",    "darkolivegreen",
                "gray",      "green",        "greenyellow",    "honeydew",
                "lawngreen", "lemonchiffon", "lightblue",
            ])))
        }
    }
}
// vim: set syntax=ron
